re
    disjunct

disjunct
    ""
    concat
    concat '|' disjunct

concat
    item
    item concat

item
    term
    class
    '(' disjunct ')'
    item postfix

postfix
    '*'
    '+'
    '?'
    '{' decimal '}'
    '{' decimal ',' '}'
    '{' decimal ',' decimal '}'

class
    '.'
    '[' elements ']'
    ;unicode_class

elements
    element
    element elements

element
    term
    term '-' term
    ;unicode_class

;unicode_class
;    '\' 'p' '{' unicode_category '}'
;    '\' 'P' '{' unicode_category '}'
;    '\' 'p' '{' unicode_script '}'
;    '\' 'P' '{' unicode_script '}'

;unicode_category
;    ; TODO: https://www.compart.com/en/unicode/category

;unicode_script
;    ; TODO: https://www.compart.com/en/unicode/scripts

; terminal nodes that are not implemented recursively,
; and correspond to one symbol
term
    char
    '\' escape

char
    '0000' . '10FFFF' - '\' - '|' - '.' - '*' - '+' - '?' - '(' - ')' - '[' - ']' - '{' - '}'

escape
    '"'
    '.'
    '\'
    '|'
    '('
    ')'
    '*'
    '+'
    '?'
    '0'
    'n'
    'r'
    't'
    'x' oct hex
    'u' '{' hex '}'
    'u' '{' hex hex '}'
    'u' '{' hex hex hex '}'
    'u' '{' hex hex hex hex '}'
    'u' '{' hex hex hex hex hex '}'
    'u' '{' hex hex hex hex hex hex '}'

decimal
    dec
    dec decimal

oct
    '0' . '7'

dec
    '0' . '9'

hex
    '0' . '9'
    'a' . 'f'
    'A' . 'F'
